# List all data products in the catalog
# Returns: product URI, label, description, owner, domain, status, dates

PREFIX dprod: <https://ekgf.github.io/dprod/>
PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dct: <http://purl.org/dc/terms/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>

SELECT ?product ?label ?description ?owner ?ownerLabel ?domain ?domainLabel ?status ?created ?modified
WHERE {
  ?product a dprod:DataProduct ;
           rdfs:label ?label ;
           dprod:dataProductOwner ?owner ;
           dprod:lifecycleStatus ?status .

  OPTIONAL { ?product dct:description ?description }
  OPTIONAL { ?product dprod:domain ?domain }
  OPTIONAL { ?product dct:created ?created }
  OPTIONAL { ?product dct:modified ?modified }
  OPTIONAL { ?owner rdfs:label ?ownerLabel }
  OPTIONAL { ?domain rdfs:label ?domainLabel }
}
ORDER BY ?label
